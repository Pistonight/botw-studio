set dotenv-load
default:
    just --list

# Init .env
initenv:
    echo "FTP_CONSOLE_IP=" >> .env
    echo "FTP_CONSOLE_PORT=5000" >> .env

# Build the module. See tools/build_wrapper.py for flags
build VERSION *FLAGS:
    mkdir -p build
    python tools/botw-link config/build.toml -V {{VERSION}} {{FLAGS}}

alias b:= build

# Deploy binary to switch
deploy:
    python tools/ftp_cli.py $FTP_CONSOLE_IP $FTP_CONSOLE_PORT \
    STOR build/botw-studio.nso atmosphere/contents/01007EF00011E000/exefs/subsdk9 \
    STOR build/botw-studio.npdm atmosphere/contents/01007EF00011E000/exefs/main.npdm

alias i:= deploy
# Remove binary on switch
remove:
    python tools/ftp_cli.py $FTP_CONSOLE_IP $FTP_CONSOLE_PORT \
    STOR atmosphere/contents/01007EF00011E000/exefs/subsdk9 \
    STOR atmosphere/contents/01007EF00011E000/exefs/main.npdm

# Swap exefs/romfs with exefs-alt and romfs-alt on switch
change-version:
    python tools/ftp_cli.py $FTP_CONSOLE_IP $FTP_CONSOLE_PORT \
    RNME atmosphere/contents/01007EF00011E000/exefs atmosphere/contents/01007EF00011E000/exefs-alt-2 \
    RNME atmosphere/contents/01007EF00011E000/romfs atmosphere/contents/01007EF00011E000/romfs-alt-2 \
    RNME atmosphere/contents/01007EF00011E000/exefs-alt atmosphere/contents/01007EF00011E000/exefs \
    RNME atmosphere/contents/01007EF00011E000/romfs-alt atmosphere/contents/01007EF00011E000/romfs \
    RNME atmosphere/contents/01007EF00011E000/exefs-alt-2 atmosphere/contents/01007EF00011E000/exefs-alt \
    RNME atmosphere/contents/01007EF00011E000/romfs-alt-2 atmosphere/contents/01007EF00011E000/romfs-alt

# Get crash reports
report:
    python tools/ftp_cli.py $FTP_CONSOLE_IP $FTP_CONSOLE_PORT \
    RTTR atmosphere/crash_reports crash_reports \
    RMD atmosphere/crash_reports

alias t:= report

# Remove build directory
clean:
    rm -rf build tools/__pycache__